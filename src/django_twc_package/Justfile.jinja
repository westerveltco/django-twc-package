set dotenv-load := true

@_default:
    just --list

bootstrap:
    python -m pip install --editable '.[dev]'

# ----------------------------------------------------------------------
# DEPENDENCIES
# ----------------------------------------------------------------------

pup:
    python -m pip install --upgrade pip

update:
    @just pup
    @just bootstrap

# ----------------------------------------------------------------------
# TESTING/TYPES
# ----------------------------------------------------------------------

test *ARGS:
    python -m nox --reuse-existing-virtualenvs --session "test" -- "{% raw %}{{ ARGS }}{% endraw %}"

testall *ARGS:
    python -m nox --reuse-existing-virtualenvs --session "tests" -- "{% raw %}{{ ARGS }}{% endraw %}"

coverage:
    python -m nox --reuse-existing-virtualenvs --session "coverage"

types:
    python -m nox --reuse-existing-virtualenvs --session "mypy"

# ----------------------------------------------------------------------
# DJANGO
# ----------------------------------------------------------------------

manage *COMMAND:
    #!/usr/bin/env python
    import sys

    try:
        from django.conf import settings
        from django.core.management import execute_from_command_line
    except ImportError as exc:
        raise ImportError(
            "Couldn't import Django. Are you sure it's installed and "
            "available on your PYTHONPATH environment variable? Did you "
            "forget to activate a virtual environment?"
        ) from exc

    settings.configure(INSTALLED_APPS=["{{ module_name }}"])
    execute_from_command_line(sys.argv + "{% raw %}{{ COMMAND }}{% endraw %}".split(" "))

alias mm := makemigrations

makemigrations *APPS:
    @just manage makemigrations {% raw %}{{ APPS }}{% endraw %}

migrate *ARGS:
    @just manage migrate {% raw %}{{ ARGS }}{% endraw %}

# ----------------------------------------------------------------------
# DOCS
# ----------------------------------------------------------------------

@docs-install:
    python -m pip install '.[docs]'

@docs-serve:
    #!/usr/bin/env sh
    just _cog
    if [ -f "/.dockerenv" ]; then
        sphinx-autobuild docs docs/_build/html --host "0.0.0.0"
    else
        sphinx-autobuild docs docs/_build/html --host "localhost"
    fi

@docs-build LOCATION="docs/_build/html":
    just _cog
    sphinx-build docs {% raw %}{{ LOCATION }}{% endraw %}

_cog:
    cog -r docs/just.md

# ----------------------------------------------------------------------
# UTILS
# ----------------------------------------------------------------------

# format justfile
fmt:
    just --fmt --unstable

# run pre-commit on all files
lint:
    python -m nox --reuse-existing-virtualenvs --session "lint"

# ----------------------------------------------------------------------
# COPIER
# ----------------------------------------------------------------------

# apply a copier template to project
copier-copy TEMPLATE_PATH DESTINATION_PATH=".":
    pipx run copier copy {% raw %}{{ TEMPLATE_PATH }}{% endraw %} {% raw %}{{ DESTINATION_PATH }}{% endraw %}

# update the project using a copier answers file
copier-update ANSWERS_FILE *ARGS:
    pipx run copier update --answers-file {% raw %}{{ ANSWERS_FILE }}{% endraw %} {% raw %}{{ ARGS }}{% endraw %}

# loop through all answers files and update the project using copier
@copier-update-all *ARGS:
    for file in `ls .copier/`; do just copier-update .copier/$file "{% raw %}{{ ARGS }}{% endraw %}"; done
